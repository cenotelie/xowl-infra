######################################################################
# Copyright (c) 2015 Laurent Wouters and others
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as
# published by the Free Software Foundation, either version 3
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General
# Public License along with this program.
# If not, see <http://www.gnu.org/licenses/>.
#
# Contributors:
#     Laurent Wouters - lwouters@xowl.org
######################################################################

@base <http://xowl.org/store/rules/owl2> .
@prefix xsd: <http://www.w3.org/2001/XMLSchema#> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix owl: <http://www.w3.org/2002/07/owl#> .
@prefix xowl: <http://xowl.org/store/rules/owl2#> .

# Rules in this document are based on those given at http://www.w3.org/TR/owl2-profiles/#Reasoning_in_OWL_2_RL_and_RDF_Graphs_using_Rules
# Inconsistency is indicated on an entity using:
#   ?e xowl:status xowl:inconsistent


# Expansion of RDF lists
#   For a RDF node h representing a list, all elements can be found with:
#   ?h xowl:contains ?e

rule xowl:list-expansion-init {
    ?h rdf:first ?e1
} => {
    ?h xowl:contains ?e1
}

rule xowl:list-expansion-aggregate {
    ?h rdf:rest ?z
    ?z xowl:contains ?e
} => {
    ?h xowl:contains ?e
}

rule xowl:list-order-init {
    ?p1 rdf:rest ?p2
} => {
    ?p2 xowl:after ?p1
}

rule xowl:list-order-transitive {
    ?p1 xowl:after ?p2
    ?p2 xowl:after ?p3
} => {
    ?p1 xowl:after ?p3
}



# Table 4. The Semantics of Equality

rule xowl:eq-sym {
    ?x owl:sameAs ?y
} => {
    ?y owl:sameAs ?x
}

rule xowl:eq-trans {
    ?x owl:sameAs ?y
    ?y owl:sameAs ?z
} => {
    ?x owl:sameAs ?z
}

rule xowl:eq-rep-s {
    ?s owl:sameAs ?s2
    ?s ?p ?o
} => {
    ?s2 ?p ?o
}

rule xowl:eq-rep-p {
    ?p owl:sameAs ?p2
    ?s ?p ?o
} => {
    ?s ?p2 ?o
}

rule xowl:eq-rep-o {
    ?o owl:sameAs ?o2
    ?s ?p ?o
} => {
    ?s ?p ?o2
}

rule xowl:eq-diff1 {
    ?x owl:sameAs ?y
    ?x owl:differentFrom ?y
} => {
    ?x xowl:status xowl:inconsistent
    ?y xowl:status xowl:inconsistent
}

rule xowl:eq-diff2 {
    ?x rdf:type owl:AllDifferent
    ?x owl:members ?list
    ?list xowl:contains ?e1
    ?list xowl:contains ?e2
    ?proxy1 rdf:first ?e1
    ?proxy2 rdf:first ?e2
    ?proxy2 xowl:after ?proxy1
    ?e1 owl:sameAs ?e2
} => {
    ?x xowl:status xowl:inconsistent
}

rule xowl:eq-diff3 {
    ?x rdf:type owl:AllDifferent
    ?x owl:distinctMembers ?y
    ?y xowl:contains ?e1
    ?y xowl:contains ?e2
    ?proxy1 rdf:first ?e1
    ?proxy2 rdf:first ?e2
    ?proxy2 xowl:after ?proxy1
    ?e1 owl:sameAs ?e2
} => {
    ?x xowl:status xowl:inconsistent
}


# Table 5. The Semantics of Axioms about Properties

rule xowl:prp-dom {
    ?p rdfs:domain ?c
    ?x ?p ?y
} => {
    ?x rdf:type ?c
}

rule xowl:prp-rng {
    ?p rdfs:range ?c
    ?p rdf:type owl:ObjectProperty
    ?x ?p ?y
} => {
    ?y rdf:type ?c
}

rule xowl:prp-fp {
    ?p rdf:type owl:FunctionalProperty
    ?x ?p ?y1
    ?x ?p ?y2
} => {
    ?y1 owl:sameAs ?y2
}

rule xowl:prp-ifp {
    ?p rdf:type owl:InverseFunctionalProperty
    ?x1 ?p ?y
    ?x2 ?p ?y
} => {
    ?x1 owl:sameAs ?x2
}

rule xowl:prp-symp {
    ?p rdf:type owl:SymmetricProperty
    ?x ?p ?y
} => {
    ?y ?p ?x
}

rule xowl:prp-trp {
    ?p rdf:type owl:TransitiveProperty
    ?x ?p ?y
    ?y ?p ?z
} => {
    ?x ?p ?z
}

rule xowl:prp-spo1 {
    ?p1 rdfs:subPropertyOf ?p2
    ?x ?p1 ?y
} => {
    ?x ?p2 ?y
}

# TODO: Implement prp-spo2 for owl:propertyChainAxiom

rule xowl:prp-eqp1 {
    ?p1 owl:equivalentProperty ?p2
    ?x ?p1 ?y
} => {
    ?x ?p2 ?y
}

rule xowl:prp-eqp2 {
    ?p1 owl:equivalentProperty ?p2
    ?x ?p2 ?y
} => {
    ?x ?p1 ?y
}

rule xowl:prp-pdw {
    ?p1 owl:propertyDisjointWith ?p2
    ?x ?p1 ?y
    ?x ?p2 ?y
} => {
    ?x xowl:status xowl:inconsistent
    ?y xowl:status xowl:inconsistent
}

rule xowl:prp-adp {
    ?x rdf:type owl:AllDisjointProperties
    ?x owl:members ?list
    ?list xowl:contains ?p1
    ?list xowl:contains ?p2
    ?proxy1 rdf:first ?p1
    ?proxy2 rdf:first ?p2
    ?proxy2 xowl:after ?proxy1
    ?x ?p1 ?y
    ?x ?p2 ?y
} => {
    ?x xowl:status xowl:inconsistent
    ?y xowl:status xowl:inconsistent
}

rule xowl:prp-inv1 {
    ?p1 owl:inverseOf ?p2
    ?x ?p1 ?y
} => {
    ?y ?p2 ?x
}

rule xowl:prp-inv2 {
    ?p1 owl:inverseOf ?p2
    ?y ?p2 ?x
} => {
    ?x ?p1 ?y
}

# TODO: Implement prp-key for owl:hasKey

rule xowl:prp-npa1 {
    ?x owl:sourceIndividual  ?i1
    ?x owl:assertionProperty ?p
    ?x owl:targetIndividual  ?i2
    ?i1 ?p ?i2
} => {
    ?x xowl:status xowl:inconsistent
}

rule xowl:prp-npa2 {
    ?x owl:sourceIndividual  ?i
    ?x owl:assertionProperty ?p
    ?x owl:targetValue       ?lt
    ?i ?p ?lt
} => {
    ?x xowl:status xowl:inconsistent
}


# Table 6. The Semantics of Classes

rule xowl:cls-nothing2 {
    ?x rdf:type owl:Nothing
} => {
    ?x xowl:status xowl:inconsistent
}

# TODO: Implement cls-int1 for owl:intersectionOf

rule xowl:cls-int2 {
    ?c owl:intersectionOf ?x
    ?x xowl:contains ?cx
    ?y rdf:type ?c
} => {
    ?y rdf:type ?cx
}

rule xowl:cls-uni {
    ?c owl:unionOf ?x
    ?x xowl:contains ?cx
    ?y rdf:type ?cx
} => {
    ?y rdf:type ?c
}

rule xowl:cls-com {
    ?c1 owl:complementOf ?c2
    ?x rdf:type ?c1
    ?x rdf:type ?c2
} => {
    ?x xowl:status xowl:inconsistent
}

rule xowl:cls-svf1 {
    ?x owl:someValuesFrom ?y
    ?x owl:onProperty ?p
    ?u ?p ?v
    ?v rdf:type ?y
} => {
    ?u rdf:type ?x
}

rule xowl:cls-svf2 {
    ?x owl:someValuesFrom owl:Thing
    ?x owl:onProperty ?p
    ?u ?p ?v
} => {
    ?u rdf:type ?x
}

rule xowl:cls-avf {
    ?x owl:allValuesFrom ?y
    ?x owl:onProperty ?p
    ?u rdf:type ?x
    ?u ?p ?v
} => {
    ?v rdf:type ?y
}

rule xowl:cls-hv1 {
    ?x owl:hasValue ?y
    ?x owl:onProperty ?p
    ?u rdf:type ?x
} => {
    ?u ?p ?y
}

rule xowl:cls-hv2 {
    ?x owl:hasValue ?y
    ?x owl:onProperty ?p
    ?u ?p ?y
} => {
    ?u rdf:type ?x
}

rule xowl:cls-maxc1 {
    ?x owl:maxCardinality "0"^^xsd:nonNegativeInteger
    ?x owl:onProperty ?p
    ?u rdf:type ?x
    ?u ?p ?y
} => {
    ?u xowl:status xowl:inconsistent
}

rule xowl:cls-maxc2 {
    ?x owl:maxCardinality "1"^^xsd:nonNegativeInteger
    ?x owl:onProperty ?p
    ?u rdf:type ?x
    ?u ?p ?y1
    ?u ?p ?y2
} => {
    ?y1 owl:sameAs ?y2
}

rule xowl:cls-maxqc1 {
    ?x owl:maxQualifiedCardinality "0"^^xsd:nonNegativeInteger
    ?x owl:onProperty ?p
    ?x owl:onClass ?c
    ?u rdf:type ?x
    ?u ?p ?y
    ?y rdf:type ?c
} => {
    ?u xowl:status xowl:inconsistent
}

rule xowl:cls-maxqc2 {
    ?x owl:maxQualifiedCardinality "0"^^xsd:nonNegativeInteger
    ?x owl:onProperty ?p
    ?x owl:onClass owl:Thing
    ?u rdf:type ?x
    ?u ?p ?y
} => {
    ?u xowl:status xowl:inconsistent
}

rule xowl:cls-maxqc3 {
    ?x owl:maxQualifiedCardinality "1"^^xsd:nonNegativeInteger
    ?x owl:onProperty ?p
    ?x owl:onClass ?c
    ?u rdf:type ?x
    ?u ?p ?y1
    ?y1 rdf:type ?c
    ?u ?p ?y2
    ?y2 rdf:type ?c
} => {
    ?y1 owl:sameAs ?y2
}

rule xowl:cls-maxqc4 {
    ?x owl:maxQualifiedCardinality "1"^^xsd:nonNegativeInteger
    ?x owl:onProperty ?p
    ?x owl:onClass owl:Thing
    ?u rdf:type ?x
    ?u ?p ?y1
    ?u ?p ?y2
} => {
    ?y1 owl:sameAs ?y2
}

rule xowl:cls-oo {
    ?c owl:oneOf ?x
    ?x xowl:contains ?y
} => {
    ?y rdf:type ?c
}


# Table 7. The Semantics of Class Axioms

rule xowl:cax-sco {
    ?c1 rdfs:subClassOf ?c2
    ?x rdf:type ?c1
} => {
    ?x rdf:type ?c2
}

rule xowl:cax-eqc1 {
    ?c1 owl:equivalentClass ?c2
    ?x rdf:type ?c1
} => {
    ?x rdf:type ?c2
}

rule xowl:cax-eqc2 {
    ?c1 owl:equivalentClass ?c2
    ?x rdf:type ?c2
} => {
    ?x rdf:type ?c1
}

rule xowl:cax-dw {
    ?c1 owl:disjointWith ?c2
    ?x rdf:type ?c1
    ?x rdf:type ?c2
} => {
    ?x xowl:status xowl:inconsistent
}

rule xowl:cax-adc {
    ?c1 rdf:type owl:AllDisjointClasses
    ?x owl:members ?list
    ?list xowl:contains ?c1
    ?list xowl:contains ?c2
    ?proxy1 rdf:first ?c1
    ?proxy2 rdf:first ?c2
    ?proxy2 xowl:after ?proxy1
    ?z rdf:type ?c1
    ?z rdf:type ?c2
} => {
    ?z xowl:status xowl:inconsistent
}


# Table 9. The Semantics of Schema Vocabulary

rule xowl:scm-cls {
    ?c rdf:type owl:Class
} => {
    ?c rdfs:subClassOf ?c
    ?c rdfs:subClassOf owl:Thing
    ?c owl:equivalentClass ?c
    owl:Nothing rdfs:subClassOf ?c
}

rule xowl:scm-sco {
    ?c1 rdfs:subClassOf ?c2
    ?c2 rdfs:subClassOf ?c3
} => {
    ?c1 rdfs:subClassOf ?c3
}

rule xowl:scm-eqc1 {
    ?c1 owl:equivalentClass ?c2
} => {
    ?c1 rdfs:subClassOf ?c2
    ?c2 rdfs:subClassOf ?c1
}

rule xowl:scm-eqc2 {
    ?c1 rdfs:subClassOf ?c2
    ?c2 rdfs:subClassOf ?c1
} => {
    ?c1 owl:equivalentClass ?c2
}

rule xowl:scm-op {
    ?p rdf:type owl:ObjectProperty
} => {
    ?p rdfs:subPropertyOf ?p
    ?p owl:equivalentProperty ?p
}

rule xowl:scm-dp {
    ?p rdf:type owl:DatatypeProperty
} => {
    ?p rdfs:subPropertyOf ?p
    ?p owl:equivalentProperty ?p
}

rule xowl:scm-spo {
    ?p1 rdfs:subPropertyOf ?p2
    ?p2 rdfs:subPropertyOf ?p3
} => {
    ?p1 rdfs:subPropertyOf ?p3
}

rule xowl:scm-eqp1 {
    ?p1 owl:equivalentProperty ?p2
} => {
    ?p1 rdfs:subPropertyOf ?p2
    ?p2 rdfs:subPropertyOf ?p1
}

rule xowl:scm-eqp2 {
    ?p1 rdfs:subPropertyOf ?p2
    ?p2 rdfs:subPropertyOf ?p1
} => {
    ?p1 owl:equivalentProperty ?p2
}

rule xowl:scm-dom1 {
    ?p rdfs:domain ?c1
    ?c1 rdfs:subClassOf ?c2
} => {
    ?p rdfs:domain ?c2
}

rule xowl:scm-dom2 {
    ?p2 rdfs:domain ?c
    ?p1 rdfs:subPropertyOf ?p2
} => {
    ?p1 rdfs:domain ?c
}

rule xowl:scm-rng1 {
    ?p rdfs:range ?c1
    ?c1 rdfs:subClassOf ?c2
} => {
    ?p rdfs:range ?c2
}

rule xowl:scm-rng2 {
    ?p2 rdfs:range ?c
    ?p1 rdfs:subPropertyOf ?p2
} => {
    ?p1 rdfs:range ?c
}

rule xowl:scm-hv {
    ?c1 owl:hasValue ?i
    ?c1 owl:onProperty ?p1
    ?c2 owl:hasValue ?i
    ?c2 owl:onProperty ?p2
    ?p1 rdfs:subPropertyOf ?p2
} => {
    ?c1 rdfs:subClassOf ?c2
}

rule xowl:scm-svf1 {
    ?c1 owl:someValuesFrom ?y1
    ?c1 owl:onProperty ?p
    ?c2 owl:someValuesFrom ?y2
    ?c2 owl:onProperty ?p
    ?y1 rdfs:subClassOf ?y2
} => {
    ?c1 rdfs:subClassOf ?c2
}

rule xowl:scm-svf2 {
    ?c1 owl:someValuesFrom ?y
    ?c1 owl:onProperty ?p1
    ?c2 owl:someValuesFrom ?y
    ?c2 owl:onProperty ?p2
    ?p1 rdfs:subPropertyOf ?p2
} => {
    ?c1 rdfs:subClassOf ?c2
}

rule xowl:scm-avf1 {
    ?c1 owl:allValuesFrom ?y1
    ?c1 owl:onProperty ?p
    ?c2 owl:allValuesFrom ?y2
    ?c2 owl:onProperty ?p
    ?y1 rdfs:subClassOf ?y2
} => {
    ?c1 rdfs:subClassOf ?c2
}

rule xowl:scm-avf2 {
    ?c1 owl:allValuesFrom ?y
    ?c1 owl:onProperty ?p1
    ?c2 owl:allValuesFrom ?y
    ?c2 owl:onProperty ?p2
    ?p1 rdfs:subPropertyOf ?p2
} => {
    ?c1 rdfs:subClassOf ?c2
}

rule xowl:scm-int {
    ?c owl:intersectionOf ?x
    ?x xowl:contains ?cx
} => {
    ?c rdfs:subClassOf ?cx
}

rule xowl:scm-uni {
    ?c owl:unionOf ?x
    ?x xowl:contains ?cx
} => {
    ?cx rdfs:subClassOf ?c
}